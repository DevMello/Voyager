plugins {
    id 'maven-publish'
    id "com.android.library" version "8.1.2"
}


project.ext {
    projectName = 'xyz.devmello'
    projectVersion = 'v1.2-ALPHA'
}

repositories {
    mavenCentral()
    google()
    maven {
        url = uri("https://git.devmello.xyz/api/packages/devmello/maven")
    }
}

allprojects {
    group = project.projectName
    version = project.projectVersion
}

dependencies {
    implementation project(':core')  // Ensure the 'core' module is correctly added
}

android {
    namespace = "xyz.devmello.voyager"
    compileSdk = 30

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant("release")
    }

    defaultConfig {
        minSdk = 23
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.projectName
            artifactId = "voyager"
            version = project.projectVersion

            afterEvaluate {
                from(components["release"])
            }
        }
    }
    // Other settings for publication
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/DevMello/Voyager")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }

        maven {
            name = "Gitea"
            url = uri("https://git.devmello.xyz/api/packages/devmello/maven")


            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token ${project.findProperty('gitea.token')}"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
